buildscript {
  configurations.classpath {
    resolutionStrategy.activateDependencyLocking()
  }
}

plugins {
  id 'codenarc'
  id 'groovy'
  id 'java-gradle-plugin'

  id 'com.gradle.plugin-publish' version '1.0.0-rc-2'
  id 'io.github.joselion.pretty-jupiter' version '2.0.1'
}

group = 'io.github.joselion'
version = '2.0.0'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

codenarc {
  reportFormat('console')
  toolVersion('3.0.1')
}

repositories {
  mavenCentral()
}

dependencyLocking {
  lockAllConfigurations()
}

dependencies {
  implementation(localGroovy())

  testImplementation(gradleTestKit())
}

gradlePlugin {
  plugins {
    strictNullCheck {
      id = 'io.github.joselion.strict-null-check'
      implementationClass = 'io.github.joselion.strictnullcheck.StrictNullCheckPlugin'
      displayName = 'Strict Null Check Plugin'
      description = 'A Gradle plugin to add full Strict Null Check to your Java code'
    }
  }
}

pluginBundle {
  website = 'https://github.com/JoseLion/strict-null-check'
  vcsUrl = 'https://github.com/JoseLion/strict-null-check.git'
  tags = ['nullability', 'strict-null', 'nonnull', 'null-check']
}

testing {
  suites {
    test {
      useSpock('2.2-M1-groovy-3.0')
    }

    e2e(JvmTestSuite) {
      useSpock('2.2-M1-groovy-3.0')

      dependencies {
        implementation(project)
      }

      sources {
        groovy {
          srcDirs = ['src/2e2/groovy']
        }
      }
    }
  }
}

tasks.named('check') {
  dependsOn(testing.suites.e2e)
}
