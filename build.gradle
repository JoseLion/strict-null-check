buildscript {
  configurations.classpath {
    resolutionStrategy.activateDependencyLocking()
  }
}

plugins {
  id 'codenarc'
  id 'groovy'
  id 'java-gradle-plugin'

  id 'com.gradle.plugin-publish' version '1.1.0'
  id 'io.github.joselion.pretty-jupiter' version '2.2.0'
}

group = 'io.github.joselion'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

codenarc {
  reportFormat('console')
  toolVersion('3.1.0')
}

repositories {
  mavenCentral()
}

dependencyLocking {
  lockAllConfigurations()
}

dependencies {
  implementation(localGroovy())

  testImplementation(gradleTestKit())
}

testing {
  suites {
    def applySpock = { suite -> suite.useSpock('2.4-M1-groovy-3.0') }

    test(applySpock)
    e2e(JvmTestSuite, applySpock)
  }
}

tasks.named('check') {
  dependsOn(testing.suites.e2e)
}

prettyJupiter {
  duration {
    customThreshold = [
      test: 75,
      e2e: 200
    ]
  }
}

pluginBundle {
  website = 'https://github.com/JoseLion/strict-null-check'
  vcsUrl = 'https://github.com/JoseLion/strict-null-check.git'
  tags = ['nullability', 'strict-null', 'nonnull', 'null-check']
}

gradlePlugin {
  plugins {
    strictNullCheck {
      id = 'io.github.joselion.strict-null-check'
      implementationClass = 'io.github.joselion.strictnullcheck.StrictNullCheckPlugin'
      displayName = 'Strict Null Check Plugin'
      description = 'A Gradle plugin to add full Strict Null Check to your Java code'
    }
  }

  testSourceSets(sourceSets.e2e)
}
